apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.danilomendes.figurines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildTypes.each {
            it.buildConfigField "String", "BASE_URL", "\"https://raw.githubusercontent.com/danilodanicomendes/figurines/static-api/static-api/\""
        }
    }

    flavorDimensions "buildType"

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        prod
    }

    // Remove mockRelease variant.
    android.variantFilter { variant ->
        println 'Build type: ' + variant.buildType.name
        if (variant.buildType.name == 'release') {
            variant.getFlavors().each { flavor ->
                println 'Flavor name: ' + flavor.name
                println 'Flavor dimension: ' + flavor.dimension
                if ('.mock'.equals(flavor.applicationIdSuffix)) {
                    println 'Ignore this variant.'
                    variant.setIgnore(true)
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$supportConstraintLayoutVersion"

    implementation "com.squareup.picasso:picasso:$picassoVersion"

    kapt "com.bluelinelabs:logansquare-compiler:$loganSquareVersion"
    implementation "com.bluelinelabs:logansquare:$loganSquareVersion"

    implementation "com.google.dagger:dagger-android-support:$daggerVersion" // if you use the support libraries
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVersion"
    implementation "com.squareup.sqlbrite2:sqlbrite:$sqlbrite"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.github.aurae.retrofit2:converter-logansquare:$retrofitLoganAdapterVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpInterceptorVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: "com.android.support", module: "support-annotations"
    })
}
repositories {
    mavenCentral()
}
